import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.github.hierynomus.license" version "0.11.0"
    id "java"
    id "maven-publish"
    id "wrapper"
    id "idea"
}

sourceCompatibility = "1.7"

defaultTasks 'clean', 'licenseFormat', 'check', 'build'

group = "com.tealcube.minecraft.bukkit"
archivesBaseName = "loot"
version = "1.0.0-SNAPSHOT"

repositories {
    mavenCentral()
    maven {
        url "http://maven.tealcube.com/artifactory/public/"
    }
    maven {
        url "https://hub.spigotmc.org/nexus/content/groups/public/"
    }
    maven {
        url "http://ci.shadowvolt.com/plugin/repository/everything/"
    }
}

dependencies {
    compile("com.tealcube.minecraft.bukkit:facecore:1.0.0") { exclude module: "bungeecord-chat" }
}

processResources {
    filter ReplaceTokens, tokens: [
            VERSION: version + "-" + (System.getenv("BUILD_NUMBER") ?: "0"),
            NAME   : System.getenv("JOB_NAME") ?: project.archivesBaseName
    ]
}

license {
    header file('HEADER')
    ignoreFailures false
    include "info/faceland/loot/**/*.java"
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

publishing {
    publications {
        java(MavenPublication) {
            artifactId = "loot"
            from components.java
        }
    }
    repositories {
        maven {
            url "http://maven.tealcube.com/artifactory/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}/"
            name "TealCube"
            credentials {
                username System.getenv("nexusUsername") ?: nexusUsername
                password System.getenv("nexusPassword") ?: nexusPassword
            }
        }
    }
}
